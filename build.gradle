buildscript {
	ext {
		springBootVersion = '1.3.1.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}") 
	}
}

plugins {
	id "org.asciidoctor.convert" version "1.5.2"
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot'

ext {
	snippetsDir = file('build/generated-snippets')
}

test {
	outputs.dir snippetsDir
}

asciidoctor {
	attributes 'snippets': snippetsDir
	inputs.dir snippetsDir
	outputDir "build/asciidoc"
	dependsOn test
	sourceDir 'src/main/asciidoc'
}

jar {
	baseName = 'weatherbus'
	if (!project.hasProperty('version') || !version) {
		version = 'SNAPSHOT'
	}

	dependsOn asciidoctor
	from ("${asciidoctor.outputDir}/html5") {
		into 'static/docs'
	}
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	mavenCentral()
	maven { url "https://repo.spring.io/snapshot" }
	maven { url "https://repo.spring.io/milestone" }
}


dependencies {
	compile('org.springframework.cloud:spring-cloud-starter-eureka')
	compile('org.springframework.cloud:spring-cloud-starter-feign')
	compile('com.google.code.gson:gson:2.3.1')
	compile('com.cedarsoftware:json-io:4.3.0')
	compile('org.projectlombok:lombok:1.16.6')
	compile('org.springframework.boot:spring-boot-starter-web')
	compile('org.springframework.boot:spring-boot-starter-data-jpa')

	runtime('com.h2database:h2')
	runtime('org.postgresql:postgresql:9.3-1101-jdbc41')

	testCompile('org.springframework.boot:spring-boot-starter-test')
	testCompile('net.javacrumbs.json-unit:json-unit:1.8.0')
	testCompile('net.javacrumbs.json-unit:json-unit-spring:1.8.0')
	testCompile('org.springframework.restdocs:spring-restdocs-mockmvc:1.0.1.RELEASE')
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-starter-parent:Brixton.M4"
	}
}

eclipse {
	classpath {
		 containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
		 containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
	}
}

task wrapper(type: Wrapper) {
	gradleVersion = '2.9'
}

task copyJar(type: Copy, dependsOn: build) {
	from jar
	into "artifacts"
}
